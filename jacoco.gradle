jacoco {
  toolVersion = "0.8.11"
}

def jacocoFiles = files([
  fileTree(
    dir: project.buildDir,
    includes: [
      "classes/kotlin/main/**"
    ],
    excludes: [
      '**/*Stub*.*'
    ]
  )
])

tasks.jacocoTestReport {
  classDirectories.from = jacocoFiles

  reports {
    xml.required = true
    html.required = true
    html.outputLocation = file("$buildDir/reports/coverage")
  }
}

tasks.jacocoTestCoverageVerification {
  afterEvaluate {
    classDirectories.from = jacocoFiles
  }

  violationRules {
    rule {
      limit {
        counter = "CLASS"
        value = "MISSEDCOUNT"
        maximum = "0".toBigDecimal()
      }

      limit {
        counter = "METHOD"
        value = "MISSEDCOUNT"
        maximum = "0".toBigDecimal()
      }
    }
  }
}

tasks.test {
  useJUnitPlatform()
  testLogging {
    events("passed", "skipped", "failed")
  }
  finalizedBy("jacocoTestReport")
  doLast {
    println("View code coverage at:")
    println("file://$buildDir/reports/coverage/index.html")
  }
}
